MAIN      START
          CALL    INPUT
          CALL    FACT
          CALL    OUTPUT
          RET
          END

;-------------------------------------------------------
; FACT(n) -> n!. args: [n, RETURN], return: [n!, RETURN]
FACT      START
          POP     GR7
          POP     GR1 ;n
          LD      GR2, GR1
          SUBA    GR2, =1 ; GR2 = n - 1
          JZE     FINISH ; n == 1 のとき
          PUSH    0, GR7
          PUSH    0, GR1
          PUSH    0, GR2
          CALL    FACT ; GR2に対してFACTを呼び出す
          CALL    MULT
          POP     GR1 ; GR1 = GR1 * GR2
          POP     GR7
FINISH    PUSH    0, GR1
          PUSH    0, GR7
          RET
          END
;-------------------------------------------------------
; OUTPUT(n) -> void. args: [n, RETURN], return: [RETURN]
OUTPUT    START
          POP     GR7
          POP     GR1 ; 表示する数n
          XOR     GR2, GR2 ; 文字数 len
LOOP      PUSH    0, GR2
          PUSH    0, GR7
          PUSH    0, GR1
          LAD     GR3, 10 ; GR3 = 10
          PUSH    0, GR3
          CALL    DIV
          POP     GR3 ; GR3 = GR1 % 10
          POP     GR1 ; GR1 = GR1 / 10
          POP     GR7
          POP     GR2
          LAD     GR4, #0030
          ADDA    GR3, GR4; 数字を文字に変換(GR3 + #0030)
          ST      GR3, OBUFINV, GR2 ; 文字を保存
          LAD     GR2, 1, GR2 ; GR2(len)++
          CPA     GR1, =0 ; a/b == 0ならば
          JZE     BREAK
          JUMP    LOOP
BREAK     ST      GR2, OLEN ; OLEN = len
; 順番入れ替え (OBUFINV -> OBUF)
          XOR     GR3, GR3 ; ループ変数 i
          SUBA    GR2, =1 ; GR2はlengthなので1多い
LOOPINV   CPA     GR2, =0
          JMI     FINISH
          LD      GR4, OBUFINV, GR2
TEST      ST      GR4, OBUF, GR3
          SUBA    GR2, =1
          ADDA    GR3, =1
          JUMP    LOOPINV
FINISH    OUT     OBUF, OLEN
          PUSH    0, GR7
          RET
OBUF      DS      6
OLEN      DS      1
OBUFINV   DS      6 ; OBUFを逆順に並べたもの
ZERO      DC      0
          END

;region-------------------------------------------------------
; INPUT() -> number. args: [RETURN], return: [number, RETURN]
INPUT     START
          POP     GR7
          IN      IBUF, ILEN
          LD      GR1, ILEN ; GR1 = i = ILEN (1桁目から始める)
          SUBA    GR1, ONE ; i -= 1
          LAD     GR2, #0001 ; GR2 = 10^i
          XOR     GR3, GR3 ; GR3 = result
          LD      GR4, MINUS
          CPA     GR4, IBUF ; GR4 == '-'
          JNZ     NEXT
          LAD     GR4, 1 ; GR4 = 負の数(1)
          ADDA    GR1, ONE ; i++
NEXT      LAD     GR4, 0 ; GR4 = 正の数(0)
; i = ILEN - 1, i >= 0; i--
LOOP      CPA     GR1, =0
          JMI     BREAK ; i < 0 なら抜ける
          LAD     GR5, #000F
          AND     GR5, IBUF, GR1 ; GR5 = i桁目の数字

          PUSH    0, GR1 ; 退避
          PUSH    0, GR3 ; 退避
          PUSH    0, GR4 ; 退避
          PUSH    0, GR7
          PUSH    0, GR2 ; 退避
          PUSH    0, GR2 ; 10^i
          PUSH    0, GR5 ; i桁目の数字
          CALL    MULT
          POP     GR5 ; かけ算結果
          POP     GR2 ; 10^i
          PUSH    0, GR5 ; GR2とGR5の順番入れ替え
          PUSH    0, GR2
          LAD     GR1, 10 ; 10をPUSH
          PUSH    0, GR1
          CALL    MULT
          POP     GR2
          POP     GR5
          POP     GR7
          POP     GR4 ; 復帰
          POP     GR3 ; 復帰
          POP     GR1 ; 復帰

          ADDA    GR3, GR5 ; result += GR5
          SUBA    GR1, ONE ; i--
          JUMP    LOOP
BREAK     PUSH    0, GR3 ; result
          PUSH    0, GR7
          RET
IBUF      DS      6       ; -32768-32767なので6
ILEN      DS      1
MINUS     DC      '-'
ZERO      DC      0
ONE       DC      1
          END

;-------------------------------------------------------
; DIV(a, b) -> [a / b, a % b]. args: [a, b, RETURN], return: [a / b, a % b, RETURN]
DIV       START
          POP     GR7 ;RETURN
          POP     GR2 ;b
          POP     GR1 ;a
          XOR     GR3, GR3 ; 商
LOOP      SUBA    GR1, GR2
          JMI     BREAK
          LAD     GR3, 1, GR3
          JUMP    LOOP
BREAK     ADDA    GR1, GR2
          PUSH    0, GR3 ; 商 a/b
          PUSH    0, GR1 ; 余り a%b
          PUSH    0, GR7 ; RETURN
          RET
          END
;-------------------------------------------------------
; MULT(a, b) -> a * b. args: [a, b, RETURN], return: [result, RETURN]
MULT      START
          POP     GR7 ; GR7 = RETURN
          POP     GR2 ; GR2 = b
          POP     GR1 ; GR1 = a

          LD      GR4, GR1 ; GR4 = 負の数かどうか
          XOR     GR4, GR2
          SRL     GR4, 15

          PUSH    0, GR1
          PUSH    0, GR4
          PUSH    0, GR7
          PUSH    0, GR2 ; b = |b|
          CALL    ABS
          POP     GR2
          POP     GR7
          POP     GR4
          POP     GR1

          PUSH    0, GR2
          PUSH    0, GR4
          PUSH    0, GR7
          PUSH    0, GR1 ; a = |a|
          CALL    ABS
          POP     GR1
          POP     GR7
          POP     GR4
          POP     GR2

          XOR     GR3, GR3 ; GR3 = result

LOOP      CPA     GR2, =0 ; b == 0 ?
          JZE     BREAK ;b == 0なら抜ける
          LD      GR5, GR2 ; bをGR4にコピー
          AND     GR5, ONE ; b &= 1
          JZE     NEXT ; b の1bit目が0ならNEXTへ
          ADDA    GR3, GR1 ; result += a を左シフトしたもの
NEXT      SRL     GR2, 1 ; b >> 1
          SLL     GR1, 1 ; a << 1
          JUMP    LOOP
BREAK     CPA     GR4, ONE ; GR4が1 (aとbのどちらかが負) ならば
          JNZ     NOTMINUS
          XOR     GR3, MASK ; GR3を負にする
          ADDA    GR3, ONE
NOTMINUS  PUSH    0, GR3 ; result
          PUSH    0, GR7 ; RETURN
          RET
ONE       DC      1
MASK      DC      #FFFF
          END

;-------------------------------------------------------
; ABS(a) -> |a|. args: [a, RETURN], return: [|a|, RETURN]
ABS       START
          POP     GR7 ; GR7 = RETURN
          POP     GR1 ; GR0 = a
          CPA     GR1, ZERO
          JPL     ONPLUS ; GR0 > 0 なら抜ける
          XOR     GR1, MASK ; 符号反転
          ADDA    GR1, ONE
ONPLUS    PUSH    0, GR1
          PUSH    0, GR7
          RET
ZERO      DC      0
ONE       DC      1
MASK      DC      #FFFF
          END